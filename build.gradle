apply plugin: 'java'

apply plugin: 'checkstyle'

checkstyle {
	ignoreFailures = true
    showViolations = false
}

task checkstyleHtml << {
	ant.xslt(in: checkstyleMain.reports.xml.destination,
			style: file('config/checkstyle/checkstyle-noframes-sorted.xsl'),
			out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
}

checkstyleMain.finalizedBy checkstyleHtml

apply plugin: 'findbugs'

findbugs {
	ignoreFailures = true
}

tasks.withType(FindBugs) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}

apply plugin: 'jacoco'

build.dependsOn jacocoTestReport

apply plugin: 'pmd'

pmd {
	ignoreFailures = true
}

tasks.withType(Pmd) {
	reports {
		xml.enabled = false
		html.enabled = true
	}
}


sourceCompatibility = 1.8
version = '1.0'

jar {
	manifest {
		attributes("Main-Class": "de.unisaarland.sopra.zugumzug.Main")
	}

	from {
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

repositories {
	maven {
		url "https://sopra.cs.uni-saarland.de/site/maven"
	}
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile group:'commons-cli', name:'commons-cli', version:'1.2'
    compile group: 'de.unisaarland.sopra', name: 'kommlib', version:'1.2'
    compile 'org.mockito:mockito-all:1.10.19'
}
